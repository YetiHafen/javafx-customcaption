plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

repositories {
    mavenCentral()
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
    configuration = 'compileOnly'
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.12.1'
    implementation 'net.java.dev.jna:jna-platform:5.12.1'

    implementation 'org.jetbrains:annotations:23.0.0'

    implementation 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

java {
    withJavadocJar()
    withSourcesJar()
}

gradle.taskGraph.whenReady { graph ->
    signing {
        required = !graph.hasTask(':core:publishToMavenLocal')
    }
}

signing {
    sign publishing.publications
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId 'net.yetihafen'
            artifactId 'javafx-customcaption'
            version rootProject.properties.version

            pom {
                name = 'javafx-customcaption'
                description = 'Custom captions (window decorations) on Windows for JavaFX'
                url = 'https://github.com/YetiHafen/javafx-customcaption'
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    url = "https://github.com/YetiHafen/javafx-customcaption"
                }
                developers {
                    developer {
                        id = 'yetihafen'
                        name = 'YetiHafen'
                        email = 'yetihafen@yetihafen.net'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releaseUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            def snapshotUrl = "https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

test {
    useJUnitPlatform()
}